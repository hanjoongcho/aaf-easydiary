apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

ext.versions = [
    /* Temporarily apply 'requestLegacyExternalStorage' options respond to Android Q 'Scoped Storage' policy */
    compileSdk        : 29
]

android {
    compileSdkVersion versions.compileSdk

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        config {
            keyAlias 'android'
            keyPassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile"))
            storePassword properties.getProperty("storePassword")
        }
    }
    
    defaultConfig {
        applicationId "me.blog.korn123.easydiary"
        minSdkVersion 16
        targetSdkVersion versions.compileSdk
        versionCode 186
        versionName '1.4.120-beta'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
        signingConfig signingConfigs.config
    }
    flavorDimensions "release"
    productFlavors {
        gms {
            dimension "release"
            signingConfig signingConfigs.config
        }
        gmsp {
            dimension "release"
            signingConfig signingConfigs.config
        }
        foss {
            dimension "release"
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        gms {
            manifest.srcFile 'src/gms/AndroidManifest.xml'
        }
        gmsp {
            manifest.srcFile 'src/gmsp/AndroidManifest.xml'
        }
        foss {
            manifest.srcFile 'src/foss/AndroidManifest.xml'
        }
    }
    buildTypes {
        release {
            minifyEnabled true // Use Android app bundle
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def releaseFileName = "easy_diary-$versionName-${new Date().format('yyyyMMddHHmmss')}.apk"
                    println("test message: $releaseFileName")
                    output.outputFileName = releaseFileName
                }
            }
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
        }
    }
    androidExtensions {
        experimental = true
    }
    packagingOptions {
        exclude 'META-INF/commons_release.kotlin_module'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // android
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.biometric:biometric:1.0.1'
    implementation 'com.google.android.material:material:1.1.0'

    // google gms api
    gmsImplementation "com.google.android.gms:play-services-auth:17.0.0"
    gmsImplementation 'com.google.http-client:google-http-client-gson:1.26.0'
    gmsImplementation('com.google.api-client:google-api-client-android:1.26.0') { exclude group: 'org.apache.httpcomponents' }
    gmsImplementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') { exclude group: 'org.apache.httpcomponents' }

    gmspImplementation "com.google.android.gms:play-services-auth:17.0.0"
    gmspImplementation 'com.google.http-client:google-http-client-gson:1.26.0'
    gmspImplementation('com.google.api-client:google-api-client-android:1.26.0') { exclude group: 'org.apache.httpcomponents' }
    gmspImplementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') { exclude group: 'org.apache.httpcomponents' }

    // google
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    gmsImplementation ('androidx.work:work-runtime-ktx:2.3.1') { exclude group: 'com.google.guava', module: 'listenablefuture' }
    gmspImplementation ('androidx.work:work-runtime-ktx:2.3.1') { exclude group: 'com.google.guava', module: 'listenablefuture' }
    fossImplementation 'androidx.work:work-runtime-ktx:2.3.1'

    // apache commons
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-lang3:3.7'

    // AAFactory
    implementation 'io.github.aafactory:commons:0.9.11'
//    implementation project(":aafactory-commons")

    // etc.
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.roomorama:caldroid:3.0.1'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'  
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'id.zelory:compressor:2.1.0'
    implementation 'com.xw.repo:bubbleseekbar:3.17'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'com.werb.pickphotoview:pickphotoview:0.4.8'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.5.2'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'jp.wasabeef:glide-transformations:4.0.0'
    implementation "jp.co.cyberagent.android:gpuimage:2.0.3"
    implementation group: 'org.apache.poi', name: 'poi', version: '3.9'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.0'
    implementation 'io.noties.markwon:core:4.1.2'

    // test
    testImplementation 'junit:junit:4.12'

    // android test
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

repositories {
    mavenCentral()
    google()
}
