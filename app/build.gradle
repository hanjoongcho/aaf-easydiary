plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'realm-android'
    id("org.jetbrains.kotlin.plugin.compose") version "2.1.10" // this version matches your Kotlin version
}

ext.versions = [
    compileSdk: 35
]

android {
    compileSdkVersion versions.compileSdk

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    signingConfigs {
        config {
            keyAlias 'android'
            keyPassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile"))
            storePassword properties.getProperty("storePassword")
        }
    }

    /**
     * @versionName: major.minor.maintenance(versionCode).hotfix(date+sequence)
     * @minSdkVersion: 19 (Android 4.4 KitKat)
     */
    defaultConfig {
        applicationId "me.blog.korn123.easydiary"
        minSdkVersion 21
        targetSdkVersion versions.compileSdk
        versionCode 334
        versionName '1.4.334.202507xx0'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
        signingConfig signingConfigs.config
//        ndk.debugSymbolLevel = 'FULL'
    }
//    ndkVersion "25.2.9519653"
    flavorDimensions += "buildType"
    productFlavors {
        gmsProd {
            dimension "buildType"
            signingConfig signingConfigs.config
        }
        gmsDev {
            dimension "buildType"
            signingConfig signingConfigs.config
        }
        foss {
            dimension "buildType"
            signingConfig signingConfigs.config
        }
        lab {
            dimension "buildType"
            signingConfig signingConfigs.config
        }
    }
    sourceSets {
        gmsProd {
            manifest.srcFile 'src/gms/AndroidManifest.xml'
            java {
                srcDirs 'src/main/java', 'src/gmsProd/java', 'src/gms/java', 'src/dummy/java'
            }
        }
        gmsDev {
            manifest.srcFile 'src/gms/AndroidManifest.xml'
            java {
                srcDirs 'src/main/java', 'src/gmsDev/java', 'src/gms/java', 'src/dummy/java'
            }
        }
        foss {
            manifest.srcFile 'src/foss/AndroidManifest.xml'
            java {
                srcDirs 'src/main/java', 'src/foss/java', 'src/dummy/java'
            }
        }
        lab {
            manifest.srcFile 'src/gms/AndroidManifest.xml'
            java {
                srcDirs 'src/main/java', 'src/gmsProd/java', 'src/gms/java', 'src/lab/java'
            }
            res {
                srcDirs 'src/gmsProd/res'
            }

        }
    }
    buildFeatures{
        dataBinding true
        viewBinding true
        compose true
    }
    buildTypes {
        release {
            minifyEnabled true // Use Android app bundle
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def releaseFileName = "easy_diary-$versionName-${new Date().format('yyyyMMddHHmmss')}.apk"
                    println(output.outputFileName)
                    println("releaseFileName: $releaseFileName")
                    output.outputFileName = releaseFileName
                }
            }
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/commons_release.kotlin_module', 'META-INF/rxjava.properties', 'META-INF/DEPENDENCIES']
        }
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }
//    composeOptions {
//        kotlinCompilerExtensionVersion = "1.5.2"
//    }
    namespace 'me.blog.korn123.easydiary'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

configurations.all {
    exclude group: 'org.jetbrains', module: 'annotations-java5'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // androidx
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.6'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation ('androidx.work:work-runtime-ktx:2.10.0') { exclude group: 'com.google.guava', module: 'listenablefuture' }
    implementation "androidx.browser:browser:1.8.0"
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // compose
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7'
    implementation 'androidx.compose.runtime:runtime-livedata:1.7.8'
    implementation 'androidx.compose.material3:material3:1.3.1'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.7.8'
    implementation 'androidx.compose.foundation:foundation-layout:1.7.8'
    implementation 'androidx.compose.ui:ui-graphics:1.7.8'
    implementation 'androidx.activity:activity-compose:1.10.1'
    debugImplementation 'androidx.compose.ui:ui-tooling:1.7.8'

    // google components for develop package (not foss)
    gmsDevImplementation 'com.google.android.gms:play-services-auth:21.3.0'
    gmsDevImplementation 'com.google.api-client:google-api-client-android:1.26.0'
    gmsDevImplementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    gmsDevImplementation 'com.google.apis:google-api-services-calendar:v3-rev411-1.25.0'
    gmsDevImplementation 'com.google.http-client:google-http-client-gson:1.26.0'

    // google components for production package (not foss)
    gmsProdImplementation 'com.google.android.gms:play-services-auth:21.3.0'
    gmsProdImplementation 'com.google.android.play:review:2.0.2'
    gmsProdImplementation 'com.google.android.play:review-ktx:2.0.2'
    gmsProdImplementation 'com.google.api-client:google-api-client-android:1.26.0'
    gmsProdImplementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    gmsProdImplementation 'com.google.apis:google-api-services-calendar:v3-rev411-1.25.0'
    gmsProdImplementation 'com.google.http-client:google-http-client-gson:1.26.0'

    // google components for lab package (not foss)
    labImplementation 'com.google.android.gms:play-services-auth:21.3.0'
    labImplementation 'com.google.android.play:review:2.0.2'
    labImplementation 'com.google.android.play:review-ktx:2.0.2'
    labImplementation 'com.google.api-client:google-api-client-android:1.26.0'
    labImplementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    labImplementation 'com.google.apis:google-api-services-calendar:v3-rev411-1.25.0'
    labImplementation 'com.google.http-client:google-http-client-gson:1.26.0'

    // google components common
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:33.0.0-android'

    // apache commons
    // From version 2.7, it calls the java.nio.file API internally.
    // NIO is available from Android 8.0 (API Level 26)
    // Therefore, we must use version 2.6 before the minimum supported Android version becomes API Level 26 or higher.
    implementation 'commons-io:commons-io:2.13.0'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'org.apache.poi:poi:3.13'

    // AAFactory
    implementation ('io.github.aafactory:commons:1.2.0') {
        exclude group: 'com.werb.eventbuskotlin', module: 'eventbuskotlin'
        exclude group: 'com.werb.moretype', module: 'moretype'
    }
//    implementation project(":aafactory-commons")

    // etc.
    implementation 'com.github.woxingxiao:BubbleSeekBar:3.20'
    implementation 'com.tbuonomo:dotsindicator:5.0'
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'id.zelory:compressor:2.1.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13' // Version Change Prohibited: This is the last version available for download from JitPack.
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.github.zhpanvip:bannerviewpager:3.5.5'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.ksoichiro:android-observablescrollview:1.5.2'
    implementation 'com.roomorama:caldroid:3.0.1'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.1'
    implementation 'org.jasypt:jasypt:1.9.2'

    implementation 'io.noties.markwon:core:4.6.2'
    implementation 'io.noties.markwon:syntax-highlight:4.6.2'
    implementation 'io.noties.markwon:ext-tables:4.6.2'
    implementation 'io.noties.markwon:image:4.6.2'
    implementation 'io.noties.markwon:html:4.6.2'
    implementation 'io.noties.markwon:ext-strikethrough:4.6.2'
    implementation 'io.noties.markwon:linkify:4.6.2'
    implementation 'io.noties:prism4j:2.0.0'
    implementation 'com.squareup:seismic:1.0.3'
    implementation "com.squareup.retrofit2:retrofit:2.11.0"
    implementation "com.squareup.retrofit2:converter-gson:2.11.0"
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    kapt 'io.noties:prism4j-bundler:2.0.0'

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // android test
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:core-ktx:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0-alpha02', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // test
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'androidx.test.ext:junit:1.2.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    testImplementation 'androidx.test.ext:truth:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
}
